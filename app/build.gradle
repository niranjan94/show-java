buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: 'extra.gradle'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

def props = new Properties()
props.load(new FileInputStream('app/keys.properties'))

props.each { key, val ->
    project.set(key, val)
}

def versionMajor = 2
def versionMinor = 1
def versionPatch = 1
def versionBuild = 2

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {

        applicationId "com.njlabs.showjava"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        resConfigs "en"
        multiDexEnabled true

        resValue "string", "app_name", "Show Java"
        buildConfigField "String", "GOOGLE_PLAY_LICENSE_KEY", GooglePlayLicenseKey
        buildConfigField "String", "IAP_PRODUCT_ID", IapProductKey
        resValue "string", "cc", cc
        resValue "string", "ii", ii
        proguardFiles 'proguard-rules.pro'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
        }
        debug {
            minifyEnabled false
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    dexOptions {
        jumboMode = true
    }

    lintOptions {
        abortOnError false
    }
    productFlavors {
        standard {
            buildConfigField "boolean", "is_travis", "false"
        }

        travis {
            buildConfigField "boolean", "is_travis", "true"
        }
    }
}

dependencies {
    // MULTI-DEX SUPPORT FOR PRE LOLLIPOP
    compile 'com.android.support:multidex:+'

    // ANDROID SUPPORT LIBRARIES
    compile 'com.android.support:appcompat-v7:+'
    compile 'com.android.support:cardview-v7:+'

    // APACHE COMMONS LIBRARIES
    compile 'commons-cli:commons-cli:1.2'
    compile 'commons-io:commons-io:2.4'

    // DEX2JAR AND IT's DEPENDENCIES
    compile 'com.google.guava:guava:18.0'
    compile 'asm:asm-all:3.3.1'
    compile files('libs/antlr-3.1.1.jar')
    compile files('libs/dex-ir-1.12.jar')
    compile files('libs/dex-reader-1.15.jar')
    compile files('libs/dex-tools-0.0.9.15.jar')
    compile files('libs/dex-translator-0.0.9.15.jar')
    compile files('libs/jsr305-1.3.9.jar')

    // CLASS FILE READER - JAVA DECOMPILER
    compile files('libs/cfr_0_110.jar')

    // APK PARSER AND BINARY XML DECODER
    compile 'net.dongliu:apk-parser:2.1.1'

    // GOOGLE PLAY SERVICES ADS SDK
    compile 'com.google.android.gms:play-services-ads:+'

    // CRASHLYTICS BUG REPORTING SDK
    compile('com.crashlytics.sdk.android:crashlytics:2.4.0@aar') {
        transitive = true;
    }

    // FLOATING ACTION BUTTON SUPPORT LIBRARY
    compile 'com.github.clans:fab:1.5.5'

    // MATERIAL DESIGN STYLED DRAWER
    compile('com.mikepenz:materialdrawer:3.1.2@aar') {
        transitive = true
    }

    // CALLIGRAPHY
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'

    // FILE PICKER
    compile 'com.nononsenseapps:filepicker:2.2.3'

    // DEPENDENCIES FOR JaDX
    compile files('libs/dx-1.10.jar')
    compile files('libs/android-5.1-clst-core.jar')
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'uk.com.robust-it:cloning:1.9.2'
    compile 'com.intellij:annotations:+@jar'

    // IN-APP PURCHASE LIBRARY v3
    compile 'com.anjlab.android.iab.v3:library:1.0.+'

    // FERNFLOWER
    compile files('libs/fernflower.jar')

    compile 'com.loopj.android:android-async-http:1.4.9'

    travisCompile project(':apl_clone')
    standardCompile project(':apl')
}